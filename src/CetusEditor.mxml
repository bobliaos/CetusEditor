<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
					   xmlns:s="library://ns.adobe.com/flex/spark" 
					   xmlns:mx="library://ns.adobe.com/flex/mx" 
					   width="1200" height="800" 
					   showStatusBar="false" fontFamily="Microsoft Yahei" backgroundColor="0" backgroundAlpha="0" addedToStage="init(event)">
	<fx:Script>
		<![CDATA[
			import com.SVGParser;
			import com.greensock.TweenLite;
			import com.lorentz.SVG.display.SVGDocument;
			import com.lorentz.SVG.display.SVGPath;
			import com.lorentz.SVG.display.base.ISVGViewBox;
			import com.lorentz.SVG.display.base.SVGElement;
			import com.lorentz.SVG.events.SVGEvent;
			import com.lorentz.SVG.parser.SVGParserCommon;
			import com.lorentz.processing.ProcessExecutor;
			
			import flash.filters.DropShadowFilter;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			
			import spark.events.IndexChangeEvent;
			import spark.events.TextOperationEvent;
			
			[Embed(source="assets/logo.png")]
			private var LogoClass:Class;
			[Bindable] private var logo:Bitmap = new LogoClass();
			[Embed(source="icons/32.png")]
			private var IconClass:Class;
			[Bindable] private var icon:Bitmap = new IconClass();
			
			[Bindable] private var _uiBackground:uint = 0x444444;	
			[Bindable] private var _mianBackground:uint = 0x222222;
			[Bindable] private var _fontColor:uint = 0xFFFFFF;
			[Bindable] private var _uiCornerRadius:uint = 4;
			[Bindable] private var floorDataProvider:ArrayCollection = new ArrayCollection([]);
			
			private var tools:Array = ["A","N","L","D","H","O","S"];
			private var toolTips:Array = ["选择工具\n使用此工具可以选择 结点/路径/图形 等,选择之后在右边面板中可以进行数据维护",
				"添加工具\n选择此工具,直接在地图上点击即可添加新结点",
				"连接工具\n选择此工具后,点击任意结点会拖出一条路径,然后点击另外一个结点便可以连接两个结点",
				"删除工具\n选择此工具后,点击 结点(蓝色)/路径 将其删除",
				"拖手工具\n选择此工具以拖曳画板",
				"打开工具\n点击此工具,将打开对话框,以打开一个图形文件",
				"保存工具\n点击此工具,保存当前编辑的图形"];
			private var selectedFilterArray:Array = [new flash.filters.DropShadowFilter(0,0,0xFF0000,1,6,6,2)];
			
			private var origXML:XML;
			private var nodes:Object = {};
			private var pathes:Array = [];
			private var displaySVGPathElements:Array;	
			private var displaySVGFloorElements:Array;
			
			private var isPathing:Boolean = false;
			private var currentTool:String = "";
			private var pathTmp:NodePath;
			
			private function init(event:Event):void
			{
				this.nativeWindow.x = (Capabilities.screenResolutionX - this.width) * 0.5;
				this.nativeWindow.y = (Capabilities.screenResolutionY - this.height) * 0.5;
				
				ProcessExecutor.instance.initialize(stage);
				addTools();
				
				setTimeout(showMainContainer,1500);
			}
			
			private function addTools():void
			{
				for(var i:int = 0;i < tools.length;i ++)
				{
					var container:BorderContainer = new BorderContainer();
					toolsContainer.addElement(container);
					container.width = 20;
					container.height = 20;
					container.setStyle("cornerRadius",4);
					container.setStyle("backgroundColor",_uiBackground);
					container.setStyle("borderVisible",false);
					container.addEventListener(MouseEvent.CLICK,onToolClick);
					
					var tool:String = tools[i];
					var toolTip:String = toolTips[i];
					
					var label:Label = new Label();
					container.addElement(label);
					label.text = tool;
					label.horizontalCenter = 0;
					label.verticalCenter = 1;
					
					label.toolTip = toolTip;
					
					styleManager.getStyleDeclaration("mx.controls.ToolTip").setStyle("fontSize","12");
					styleManager.getStyleDeclaration("mx.controls.ToolTip").setStyle("fontFamily","Microsoft Yahei");
					styleManager.getStyleDeclaration("mx.controls.ToolTip").setStyle("color","#0x000000");
					styleManager.getStyleDeclaration("mx.controls.ToolTip").setStyle("backgroundColor","#FFFF00");
				}
				
				selectToolByChar("A");
				
				displayContainer.addEventListener(MouseEvent.MOUSE_DOWN,onDisplayContainerMouseDown);
				this.stage.addEventListener(KeyboardEvent.KEY_DOWN,function(e:KeyboardEvent):void{
						var char:String = String.fromCharCode(e.charCode).toUpperCase();
						if(tools.indexOf(char) > -1)
							selectToolByChar(char);
				});
			}
			
			private function onDisplayContainerMouseDown(event:MouseEvent):void
			{
				displayContainer.setFocus();
				var obj:* = event.target;	//NODE,PATH,SVGPATH,STAGE
				switch(currentTool){
					case tools[0]:
						selectTarget(obj);
						if(obj is Node)
						{
							var dragNode:Node = obj;
							
							dragNode.startDrag();
							var updatePathes:Function = function(e:Event):void{
								for each(var path:NodePath in dragNode.pathes) path.updateDisplay();
							};
							var dragOver:Function = function(e:MouseEvent):void{
								dragNode.stopDrag();
								displayContainer.removeEventListener(MouseEvent.MOUSE_OUT,dragOver);
								displayContainer.removeEventListener(MouseEvent.MOUSE_UP,dragOver);
								displayContainer.removeEventListener(Event.ENTER_FRAME,updatePathes);
								updatePathes(null);
								
								var nodePosition:String = dragNode.x + "," + dragNode.y;
								var nodeId:String = dragNode.nodeId;
								origXML.children().(@id == floorDownList.selectedItem)[0].children().(@nodeId == nodeId)[0].@nodePosition = nodePosition;
								
								updatePanelDisplay();
							};
							
							displayContainer.addEventListener(MouseEvent.MOUSE_OUT,dragOver);
							displayContainer.addEventListener(MouseEvent.MOUSE_UP,dragOver);
							displayContainer.addEventListener(Event.ENTER_FRAME,updatePathes);
						}
						break;
					case tools[1]:
						if(!(obj is Node) && floorDownList.selectedItem)
						{
							var pathNodeXML:XML = SVGParser.generateNodeXML(displayContainer.mouseX,displayContainer.mouseY);
							origXML.children().(@id == floorDownList.selectedItem)[0].appendChild(pathNodeXML);
							var node:Node = new Node(pathNodeXML,floorDownList.selectedItem);
							nodesContainer.addChild(node);
							nodes[node.nodeId] = node;
							selectTarget(node);
						}
						break;
					case tools[2]:
						if(obj is Node)
						{
							selectTarget(obj);
							if(isPathing) {
								isPathing = false;
								displayContainer.removeEventListener(MouseEvent.MOUSE_MOVE,onDisplayContainerMouseMove);
								
								var path:NodePath = new NodePath(pathTmp.startNode,obj);
								pathTmp.parent.removeChild(pathTmp);
								pathTmp = null;
								
								pathesContainer.addChild(path);
								path.startNode.pathes.push(path);
								path.endNode.pathes.push(path);
								pathes.push(path);
								
								var startNodeId:String = path.startNode.nodeId;
								var endNodeId:String = path.endNode.nodeId;
								var curSelectedFloorId:String = floorDownList.selectedItem;
								
								var str:String;
								
								str = origXML.children().(@id == curSelectedFloorId).children().(@nodeId == startNodeId)[0].@bindNodeIds + "," + endNodeId;
								if(str.charAt(0) == ",") str = str.substr(1,str.length - 1);
								origXML.children().(@id == curSelectedFloorId).children().(@nodeId == startNodeId)[0].@bindNodeIds = str;
								
								str = origXML.children().(@id == curSelectedFloorId).children().(@nodeId == endNodeId)[0].@bindNodeIds + "," + startNodeId;
								if(str.charAt(0) == ",") str = str.substr(1,str.length - 1);
								origXML.children().(@id == curSelectedFloorId).children().(@nodeId == endNodeId)[0].@bindNodeIds = str;
							}
							else
							{
								isPathing = true;
								pathTmp = new NodePath(obj,new Node(SVGParser.generateNodeXML(displayContainer.mouseX,displayContainer.mouseY),floorDownList.selectedItem))
								pathesContainer.addChild(pathTmp);
								displayContainer.addEventListener(MouseEvent.MOUSE_MOVE,onDisplayContainerMouseMove);
							}
						}
						break;
					case tools[3]:
						if(obj is Node && obj.sourceXML.@nodeTypeId == "0")
						{
							var deleteNode:Node = obj;
							
							//删除PATH
							var tmpPathes:Array = deleteNode.pathes.concat();
							for each(var deleteSubPath:NodePath in tmpPathes) deletePath(deleteSubPath);
							tmpPathes = null;
							
							//删除显示
							nodesContainer.removeChild(deleteNode);
							
							//删除数据
							var deleteNodeId:String = deleteNode.nodeId;
							nodes[deleteNodeId] = null;
							delete nodes[deleteNodeId];
							
							//删除XML
							delete origXML.children().(@id == floorDownList.selectedItem).children().(@nodeId == deleteNodeId)[0];
						}
						else if(obj is NodePath)
						{
							deletePath(obj);
						}
						break;
					case tools[4]:
						displayContainer.startDrag();
						displayContainer.addEventListener(MouseEvent.MOUSE_UP,function(e:MouseEvent):void{
							displayContainer.stopDrag();
							displayContainer.removeEventListener(MouseEvent.MOUSE_UP,arguments.callee);
						});
						break;
					default:break;
				}
			}
			
			private function addNewDisplayPath(startPathNode:Node,bindNode:Node,visible:Boolean = false):void
			{
				var newPath:NodePath = new NodePath(startPathNode,bindNode);
				
				startPathNode.addPath(newPath);
				bindNode.addPath(newPath);
				
				pathesContainer.addChild(newPath);
				newPath.visible = visible;
				
				pathes.push(newPath);
			}
			
			private function deletePath(path:NodePath):void
			{
				path.startNode.pathes.splice(path.startNode.pathes.indexOf(path),1);
				path.endNode.pathes.splice(path.endNode.pathes.indexOf(path),1);
				
				if(path.parent) path.parent.removeChild(path);
				
				pathes.splice(pathes.indexOf(path),1);
				
				var startNodeId:String = path.startNode.nodeId;
				var endNodeId:String = path.endNode.nodeId;
				var curSelectedFloorId:String = floorDownList.selectedItem;
				
				var str:String;
				
				str = origXML.children().(@id == curSelectedFloorId).children().(@nodeId == startNodeId)[0].@bindNodeIds;
				str = str.replace(endNodeId,"");
				str = str.replace(",,",",");
				origXML.children().(@id == curSelectedFloorId).children().(@nodeId == startNodeId)[0].@bindNodeIds = str;
				
				str = origXML.children().(@id == curSelectedFloorId).children().(@nodeId == endNodeId)[0].@bindNodeIds;
				str = str.replace(startNodeId,"");
				str = str.replace(",,",",");
				origXML.children().(@id == curSelectedFloorId).children().(@nodeId == endNodeId)[0].@bindNodeIds = str;
				
				path = null;
			}
			
			private var currentSelectedNode:Node = null;
			
			private function clearCurSelectedNode():void
			{
				currentSelectedNode = null;
				
				nodeIdInput.text = "";
				nodeTypeIdInput.text = "";
				bindNodeIdsInput.text = "";
				bindShopIdInput.text = "";
				nodePositionInput.text = "";
				fillInput.text = "";
				deepInput.text = "";
				dInput.text = "";
			}
			
			private function selectTarget(obj:*):void
			{
				clearCurSelectedNode();
				
				for each(var node:Node in nodes) 
				{
					node.filters = (node == obj) ? selectedFilterArray : [];
					if(node == obj) currentSelectedNode = node;
				}
				for each(var element:SVGPath in displaySVGPathElements)
				{
					element.filters = (element == obj) ? selectedFilterArray : [];
					if(element == obj)
					{
						currentSelectedNode = nodes[element.id];
					}
				}
				for each(var path:NodePath in pathes) path.filters = (path == obj) ? selectedFilterArray : [];
				
				updatePanelDisplay();
			}
			
			private function updatePanelDisplay():void
			{
				if(currentSelectedNode)
				{
					var curEditingNodeXML:XML = currentSelectedNode.sourceXML;
					nodeIdInput.text = curEditingNodeXML.@nodeId;
					nodeTypeIdInput.text = curEditingNodeXML.@nodeTypeId;
					bindNodeIdsInput.text = curEditingNodeXML.@bindNodeIds;
					bindShopIdInput.text = curEditingNodeXML.@bindShopId;
					nodePositionInput.text = curEditingNodeXML.@nodePosition;
					fillInput.text = curEditingNodeXML.@fill;
					deepInput.text = curEditingNodeXML.@deep;
					dInput.text = curEditingNodeXML.@d;
				}
			}
			
			private function onDisplayContainerMouseMove(e:MouseEvent):void
			{
				pathTmp.endNode.x = displayContainer.mouseX;
				pathTmp.endNode.y = displayContainer.mouseY;
				pathTmp.updateDisplay();
			}
			
			private function onToolClick(event:MouseEvent):void
			{
				var char:String = (event.currentTarget.getElementAt(0) as Label).text;
				selectToolByChar(char);
			}
			
			private function selectToolByChar(char:String):void
			{
				unSelectedAllTools();
				for(var i:int = 0;i < toolsContainer.numElements;i ++)
				{
					var tmpContainer:BorderContainer = toolsContainer.getElementAt(i) as BorderContainer;
					if((tmpContainer.getElementAt(0) as Label).text == char)
					{
						tmpContainer.setStyle("backgroundColor",_mianBackground);
						currentTool = char;
					}
				}
				
				if(char == tools[5])
				{
					openFile();
					selectToolByChar("A");
				}
				else if(char == tools[6])
				{
					saveFile();
					selectToolByChar("A");
				}
			}
			
			private function unSelectedAllTools():void
			{
				currentTool = "";
				for(var i:int = 0;i < toolsContainer.numElements;i ++)
				{
					var tmpContainer:BorderContainer = toolsContainer.getElementAt(i) as BorderContainer;
					tmpContainer.setStyle("backgroundColor",_uiBackground);
				}
			}
			
			private function showMainContainer():void
			{
				TweenLite.to(splashContainer,0.5,{alpha:0});
				mainShadowContainer.visible = true;
				mainShadowContainer.alpha = 0;
				TweenLite.to(mainShadowContainer,0.5,{alpha:1});
			}
			
			private function openFile():void
			{
				if(origXML)
				{
					Alert.show("你已经加载过了,要加载新的文件请重启,么么哒","呵呵");
					return;
				}
				
				var file:File = new File();
				file.browseForOpen("打开SVG文件"/*,[new FileFilter("SVG Files","*.svg")]*/);
				file.addEventListener(Event.SELECT,function(e:Event):void{
					var fs:FileStream = new FileStream();
					fs.open(file,FileMode.UPDATE);
					var origSVGString:String = fs.readUTFBytes(fs.bytesAvailable);
					fs.close();
					
					try{
						origXML = SVGParser.coverToAllPath(isJSONCheckBox.selected ? (JSON.parse(origSVGString) as String) : origSVGString);
					}catch(e:Error){
						Alert.show("哎唷,崩溃了!\n工具栏第一个框,勾选加载JSON格式,不勾选加载XML格式!\n不用谢!","呵呵");
						return;
					}
					
					nodes = {};
					for each(var floorXML:XML in origXML.children())
					{
						for each(var pathXML:XML in floorXML.children())
						{
							var nodeId:String = pathXML.@nodeId.toString();
							var node:Node = new Node(pathXML,floorXML.@id);
							nodesContainer.addChild(node);
//							logosContainer.addChild(node.logo);
							node.visible = false;
							nodes[nodeId] = node;
						}
					}
					
					pathes = [];
					for each(var startPathNode:Node in nodes)
					{
						var startNodeBindNodeIdString:String = startPathNode.sourceXML.@bindNodeIds;
						var startNodeBindNodeIds:Array = startNodeBindNodeIdString.split(",");
						for each(var bindNodeId:String in startNodeBindNodeIds)
						{
							var bindNode:Node = nodes[bindNodeId];
							if(bindNode)
							{
								var isExist:Boolean = false;
								for each(var path:NodePath in startPathNode.pathes)
								{
									if(path.startNode == startPathNode && path.endNode == bindNode) isExist = true;
									if(path.startNode == bindNode && path.endNode == startPathNode) isExist = true;
								}
								
								if(!isExist)
								{
									addNewDisplayPath(startPathNode,bindNode);
								}
							}
						}
					}
					
					displaySVGFloorElements = [];
					displaySVGPathElements = [];
					
					mainSVG = new SVGDocument();
					mainSVG.parse(origXML);
					mainSVG.addEventListener(SVGEvent.ELEMENT_ADDED,function(e:SVGEvent):void{
						var elementClassName:String = getQualifiedClassName(e.element).split(":")[2];
						switch(elementClassName){
							case "SVG":			//SVG
								displayContainer.graphics.beginFill(0x888888,1);
								displayContainer.graphics.drawRect(0,0,(e.element as ISVGViewBox).svgViewBox.width,(e.element as ISVGViewBox).svgViewBox.height);
								displayContainer.graphics.endFill();
								break;	
							case "SVGG":		//SVG FLOOR CONTAINER
								floorDataProvider.source.push(e.element.id);
								displaySVGFloorElements.push(e.element);
								e.element.visible = false;
								break;
							case "SVGPath":		//SVG PATH
								displaySVGPathElements.push(e.element);
								break;
							default:break;
						}
					});
					
					SVGContainer.addChild(mainSVG);
				});
			}
			
			private var mainSVG:SVGDocument;

			private function saveFile():void
			{
				if(origXML)
				{
					var fileR:FileReference = new FileReference();
					var saveString:String = "<?xml version='1.0' encoding='utf-8'?>\n" +
						"<!-- Generator: GimSVGEditor 1.0.1 -->\n" +
						"<!DOCTYPE svg PUBLIC '-//W3C//DTD SVG 1.1//EN' 'http://www.w3.org/Graphics/SVG/1.1/DTD/svg11.dtd'>\n" +
						origXML.toXMLString();
					
					fileR.save( isJSONCheckBox.selected ? JSON.stringify(saveString) : saveString,isJSONCheckBox.selected ? "data.json" : "data.sgxml");
				}
				else
				{
					Alert.show("哎,智商拙计啊...\n请加载一个SVG文件先","o(︶︿︶)o");
				}
			}
			
			private function onFloorDownListChange(event:IndexChangeEvent):void
			{
				for each(var floor:SVGElement in displaySVGFloorElements)
				{
					floor.visible = false;
				}
				displaySVGFloorElements[event.newIndex].visible = true;
				var floorId:String = displaySVGFloorElements[event.newIndex].id;
				for each(var node:Node in nodes)
				{
					node.visible = node.floorId == floorId;
				}
				
				for each(var path:NodePath in pathes)
				{
					path.visible = path.startNode.floorId == path.endNode.floorId && path.startNode.floorId == floorId;
				}
			}
			
			protected function onInputChangee(event:TextOperationEvent):void
			{
				if(currentSelectedNode)
				{
					var nodeId:String = currentSelectedNode.nodeId;
					var floorId:String = currentSelectedNode.floorId;
					
					origXML.children().(@id == floorId).children().(@nodeId == nodeId)[0].@nodeTypeId = nodeTypeIdInput.text;
					origXML.children().(@id == floorId).children().(@nodeId == nodeId)[0].@bindNodeIds = bindNodeIdsInput.text;
					origXML.children().(@id == floorId).children().(@nodeId == nodeId)[0].@bindShopId = bindShopIdInput.text;
					origXML.children().(@id == floorId).children().(@nodeId == nodeId)[0].@nodePosition = nodePositionInput.text;
					origXML.children().(@id == floorId).children().(@nodeId == nodeId)[0].@fill = fillInput.text;
					origXML.children().(@id == floorId).children().(@nodeId == nodeId)[0].@deep = deepInput.text;
					origXML.children().(@id == floorId).children().(@nodeId == nodeId)[0].@d = dInput.text;
					
					currentSelectedNode.updateDisplay();
				}
			}
			
			protected function onLinkBtnClick(event:MouseEvent):void
			{
				var nodeIdStart:String = nodeIdStartInput.text;
				var nodeIdEnd:String = nodeIdEndInput.text;
				
				var startNode:Node = nodes[nodeIdStart];
				var endNode:Node = nodes[nodeIdEnd];
				
				if(startNode && endNode && startNode != endNode)
				{
					var newPath:NodePath = new NodePath(startNode,endNode);
					
					startNode.addPath(newPath);
					endNode.addPath(newPath);
					
					pathesContainer.addChild(newPath);
					newPath.visible = startNode.floorId == endNode.floorId;
					
					pathes.push(newPath);
					
					var str:String;
					
					str = startNode.sourceXML.@bindNodeIds + "," + endNode.nodeId;
					if(str.charAt(0) == ",") str = str.substr(1,str.length - 1);
					startNode.sourceXML.@bindNodeIds = str;
					
					str = endNode.sourceXML.@bindNodeIds + "," + startNode.nodeId;
					if(str.charAt(0) == ",") str = str.substr(1,str.length - 1);
					endNode.sourceXML.@bindNodeIds = str;
					
					Alert.show("关联成功,路径类型:" + ((startNode.floorId == endNode.floorId) ? "同层" : "跨层"),"么么哒");
				}
			}
			
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<s:Group id="splashContainer" horizontalCenter="0" verticalCenter="0">
		<s:Image source="{logo}" horizontalCenter="0" verticalCenter="0" smooth="true"/>
		<s:Label y="116" left="93" right="140" color="0xDDDDDD"
				 text="loading..." textAlign="left"
				 verticalAlign="middle"/>
	</s:Group>
	<s:Group id="mainShadowContainer" width="100%" height="100%" visible="false" color="{_fontColor}" horizontalCenter="0" verticalCenter="0">
		<s:BorderContainer cornerRadius="{_uiCornerRadius + 2}" backgroundColor="{_mianBackground}" left="0" right="0" top="0" bottom="0" borderWeight="3" borderAlpha="0.6" borderColor="0x222222">
			
			<s:BorderContainer cornerRadius="{_uiCornerRadius}" id="titleContainer" left="2" right="2" top="2" height="24" horizontalCenter="0" backgroundColor="{_uiBackground}" mouseDown="{nativeWindow.startMove();}">
				<s:Image source="{icon}" left="-4" top="2" bottom="0" smooth="true" toolTip="办证 18682004968"/>
				<s:Label verticalCenter="0" left="24" text="CetusEditor - v0.0.1"/>
				<s:BorderContainer backgroundColor="0xFFFFFF" borderVisible="false" cornerRadius="6" right="2" top="2" bottom="2" width="20" height="20" click="{this.nativeApplication.exit();}" toolTip="虽然我长的丑了点,但我确实是关闭按钮 点我之前,我还有话要说,你确定你已经保存了?如果没有请点击左边工具栏 S 工具"/>
			</s:BorderContainer>
			
			<s:BorderContainer id="displayContainerMask" cornerRadius="{_uiCornerRadius}" top="28" left="2" right="2" bottom="2" backgroundColor="{_uiBackground}"/>
			<s:Group id="displayContainer" mask="{displayContainerMask}">
				<mx:UIComponent id="SVGContainer"/>
				<s:Group id="logosContainer"/>
				<mx:UIComponent id="pathesContainer"/>
				<mx:UIComponent id="nodesContainer"/>
			</s:Group>
			
			<s:BorderContainer id="panelTool" cornerRadius="{_uiCornerRadius}" width="24" top="28" left="2" backgroundColor="{_uiBackground}">
				<s:BorderContainer cornerRadius="{_uiCornerRadius - 1}" backgroundColor="{_uiBackground}" width="22" height="12" mouseDown="{panelTool.startDrag();}" mouseUp="{panelTool.stopDrag();}" toolTip="你敢拖我就敢动"/>
				<s:CheckBox selected="true" id="isJSONCheckBox" horizontalCenter="2" top="12" toolTip="勾选进行JSON格式的 打开/保存,反选进行XML格式的 打开/保存" />
				<s:VGroup id="toolsContainer" width="100%" horizontalAlign="center" top="32" bottom="4" gap="1"/>
			</s:BorderContainer>
			
			<s:BorderContainer id="panelFloor" cornerRadius="{_uiCornerRadius}" width="240" top="28" right="2" backgroundColor="{_uiBackground}">
				<s:BorderContainer cornerRadius="{_uiCornerRadius - 1}" backgroundColor="{_uiBackground}" width="238" height="12" mouseDown="{panelFloor.startDrag();}" mouseUp="{panelFloor.stopDrag();}" toolTip="越脱越感动"/>
				<s:DropDownList id="floorDownList" prompt="Select Floor" width="100%" dataProvider="{floorDataProvider}" top="16" change="onFloorDownListChange(event)" toolTip="选择楼层"/>
				<s:VGroup left="2" right="2" top="40" bottom="2" gap="0">
					<s:HGroup width="100%"><s:Label height="100%" verticalAlign="middle" width="30" text="ID"/><s:TextInput id="nodeIdInput" change="onInputChangee(event)" width="100%" editable="false"/></s:HGroup>
					<s:HGroup width="100%"><s:Label height="100%" verticalAlign="middle" width="30" text="类型"/><s:TextInput id="nodeTypeIdInput" change="onInputChangee(event)" width="100%" toolTip="类型列表:0:寻路结点,1:地形,2:店铺,3:机器位置,4:扶梯,5:升降梯,6:楼梯,7:洗手间,8:服务台,9:自动提款机"/></s:HGroup>
					<s:HGroup width="100%"><s:Label height="100%" verticalAlign="middle" width="30" text="路径"/><s:TextInput id="bindNodeIdsInput" change="onInputChangee(event)" width="100%" editable="false"/></s:HGroup>
					<s:HGroup width="100%"><s:Label height="100%" verticalAlign="middle" width="30" text="店铺"/><s:TextInput id="bindShopIdInput" change="onInputChangee(event)" width="100%" toolTip="选中结点/区域所对应的店铺号"/></s:HGroup>
					<s:HGroup width="100%"><s:Label height="100%" verticalAlign="middle" width="30" text="位置"/><s:TextInput id="nodePositionInput" change="onInputChangee(event)" width="100%" toolTip="结点位置,对于区域来说,就是其文字/图标贴图位置,调整此位置能够调整贴图位置"/></s:HGroup>
					<s:HGroup width="100%"><s:Label height="100%" verticalAlign="middle" width="30" text="填充"/><s:TextInput id="fillInput" change="onInputChangee(event)" width="100%" editable="false"/></s:HGroup>
					<s:HGroup width="100%"><s:Label height="100%" verticalAlign="middle" width="30" text="深度"/><s:TextInput id="deepInput" change="onInputChangee(event)" width="100%" toolTip="区域生成3D模型后的高度"/></s:HGroup>
					<s:HGroup width="100%"><s:Label height="100%" verticalAlign="middle" width="30" text="数据"/><s:TextInput id="dInput" change="onInputChangee(event)" width="100%" editable="false"/></s:HGroup>
					
					<s:Group width="100%" height="20"/>
					<s:Label text="结点关联:"/>
					<s:HGroup width="100%"><s:TextInput id="nodeIdStartInput" width="100%"/></s:HGroup>
					<s:HGroup width="100%"><s:TextInput id="nodeIdEndInput" width="100%"/></s:HGroup>
					<s:HGroup width="100%"><s:Button label="点击连接结点" width="100%" click="onLinkBtnClick(event)" toolTip="哎,你也是蛮拼的,不过这个工具跟你没什么关系,放开它让我来"/></s:HGroup>
				</s:VGroup>
			</s:BorderContainer>
		</s:BorderContainer>
	</s:Group>
</s:WindowedApplication>
